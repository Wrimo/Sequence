<S> ::= <StatementList> NEWLINE | NEWLINE

# statement level constructions
<StatementList> ::= <Statement> NEWLINE <StatementList> | <Statement>
<Statement> ::= <PrintState> | <AssignState> | <IfState> | <BeginState> | <ExpectState> | <RevealState>
<CodeBlock> ::= RBRACKET NEWLINE <StatementList> NEWLINE LBRACKET
<RevealState> ::= REVEAL RPAREN IDENTIFIER LPAREN
<PrintState> ::= PRINT RPAREN <Expr> LPAREN
<AssignState> ::= IDENTIFIER ASSIGNMENT <Expr>
<IfState> ::= IF <Expr> <CodeBlock> | IF <Expr> <CodeBlock> <ElseList>

<ElseList> ::= <ElseIf> <ElseList> | <ElseIf> | <Else>
<ElseIf> ::= VERTICALBAR <Expr> <CodeBlock> 
<Else> ::= VERTICALBAR <CodeBlock>


<BeginState> ::= BEGIN <CodeBlock>
<ExpectState> ::= EXPECT <Expr> <CodeBlock>

# grammar for expressions
<Expr> ::= <Expr> <LogOp> <Expr1> | NOT <Expr1> | <Expr1>
<Expr1> ::= <Expr1> <CompOp> <Expr2> | <Expr2>
<Expr2> ::= <Expr2> <AddOp> <Term> | <Term>
<Term> ::= <Term> <MulOp> <Term2> | <Term2>
<Term2> ::= FACTORIAL <Factor> | <Term2> EXPONENT <Factor> | <Factor>
<Factor> ::= INTEGER | IDENTIFIER | FLOAT | <Bool> | PREV IDENTIFIER | RPAREN <Expr> LPAREN
<CompOp> ::= EQUALOP | NOTEQUALOP | GTHANOP | GETHANOP | LTHANOP | LETHANOP
<LogOp> ::= AND | OR
<MulOp> ::= MULOP | DIVOP | MODOP
<AddOp> ::= ADDOP | SUBOP

<Bool> ::= TRUE | FALSE